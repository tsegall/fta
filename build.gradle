def configureVersions() {
	def versions = new Properties()
	versions.load(new FileInputStream("$project.rootDir/versions.properties"))
	versions.each { prop -> project.ext.set(prop.key, prop.value) }
}

apply plugin: "com.github.ben-manes.versions"

buildscript {
	repositories {
		gradlePluginPortal()
	}

	dependencies {
		classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
	}
}

subprojects {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}

	// We should always build a Java 8 compatible library
	if (JavaVersion.current().java9Compatible) {
		project.afterEvaluate {
			tasks.withType(JavaCompile) {
				options.compilerArgs.addAll(['--release', 8])
			}
		}
	}

	apply plugin: "java"
	apply plugin: 'eclipse'
	apply plugin: 'jacoco'
	apply plugin: 'java-library-distribution'
	apply plugin: 'jacoco'
	apply plugin: 'signing'
	apply plugin: 'maven-publish'

	configureVersions()

	version = "8.0.6"
	group = "com.cobber.fta"

	java {
		withSourcesJar()
		withJavadocJar()
	}

	artifacts {
		archives javadocJar, sourcesJar
	}

	jar {
		manifest {
			attributes(
				'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Implementation-Version': archiveVersion
			)
		}
	}

	javadoc {
		options.noQualifiers 'java.lang:java.util:com.cobber.fta:com.cobber.fta.core'
	}


	test {
		// enable TestNG support (default is JUnit)
		def groups = System.getProperty('groups', 'all')
			useTestNG() {
				includeGroups groups
			}
		dependsOn cleanTest
		testLogging.showStandardStreams = true
	}

	publishing {
		repositories {
			maven {
				name = "ossrh"
				url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
				credentials {
					username = findProperty('ossrhUsername')
					password = findProperty('ossrhPassword')
				}
			}
		}

		publications {
			mavenJava(MavenPublication) {
			    switch(artifactId) {
				case 'core':
					artifactId = 'fta-core'
					break
				case 'types':
					artifactId = 'fta'
					break
				default:
					break
				}
				from components.java

				pom {
					name = 'Fast Text Analyzer'
					packaging = 'jar'
					description = 'Analyze Text data to determine simple type and Semantic type information as well as other key metrics associated with a text stream.'
					url = 'https://github.com/tsegall/fta'

					scm {
					    connection = 'scm:git:https://github.com/tsegall/fta.git'
					    developerConnection = 'scm:git:https://github.com/tsegall/fta.git'
					    url = 'https://github.com/tsegall/fta.git'
					}

				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id = 'tsegall'
						name = 'Tim Segall'
						email = 'xtim@cobber.com'
					}
				}
			    }
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}
}
